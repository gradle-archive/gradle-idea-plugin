// For deploying the plugin you need to set the following properties:
final String ARTIFACTORY_ID = 'gradleIntellijPlugin_artifactoryId'
final String ARTIFACTORY_PW = 'gradleIntellijPlugin_artifactoryPassword'

buildscript {
    repositories {
        mavenRepo urls: 'http://gradle.artifactoryonline.com/gradle/plugins'
    }
    dependencies {
        classpath "org.gradle.plugins:gradle-idea-plugin:0.2"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.gradle.idea'
 
group = 'org.gradle.plugins'
version = '0.3.1' // + new java.text.SimpleDateFormat('yyyyMMddHHmmssZ').format(new Date())

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.8.1', "org.spockframework:spock-core:0.4-groovy-1.7@jar"
    groovy localGroovy()
    deployerJars "org.apache.maven.wagon:wagon-webdav-jackrabbit:1.0-beta-6"
}

task libs(type: Copy) {
    from configurations.testRuntime
    into 'libs'
}

uploadArchives {
    doFirst {
       // TODO: is there a better thing to do than throw "Exception" here?
       if (!hasProperty(ARTIFACTORY_ID) || !hasProperty(ARTIFACTORY_PW))
         throw new Exception("You must specify both $ARTIFACTORY_ID and $ARTIFACTORY_PW to upload archives")
    }

    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "dav:http://gradle.artifactoryonline.com/gradle/plugins-releases-local") {
            authentication(userName: properties[ARTIFACTORY_ID], properties[ARTIFACTORY_PW])
        }
    }
}

ideaProject {
    javaVersion = '1.6'
    withXml { root ->
        root.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
    }
}